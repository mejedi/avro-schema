# Maintainer: Daniel YC Lin <dlin.tw@gmail.com>

pkgname=tarantool-avro-schema
_pkgname=avro-schema
pkgver=2.0
pkgrel=1
pkgdesc='Apache Avro schema tools for Tarantool'
arch=(i686 x86_64)
url='http://github.com/tarantool/avro-schema'
license=(BSD)
makedepends=(cmake tarantool)
source=(https://github.com/tarantool/$_pkgname/archive/$pkgver.tar.gz)

build() {
  cd $_pkgname-$pkgver

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_BINDIR=/usr/bin \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    .

  make
}

check() {
  cd $_pkgname-$pkgver
  make check
}

package() {
  cd $_pkgname-$pkgver
  make install DESTDIR="$pkgdir"

  install -D -m644 lib/phf/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
md5sums=('eace51cb4caf4666241cb7dbf80a4cff')
